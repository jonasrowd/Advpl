#INCLUDE "MATR300.CH" 
#INCLUDE "PROTHEUS.CH"
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MATR300  ³ Autor ³Alexandre Inacio Lemes ³ Data ³21/06/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relacao das movimentacoes internas                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MATR300(void)                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
User Function FMATR300()

Local oReport

//If FindFunction("TRepInUse") .And. TRepInUse()
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Interface de impressao                                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oReport:= ReportDef()
	oReport:PrintDialog()
//Else
//	MATR300R3()
//EndIf
                                               
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ ReportDef³Autor  ³Alexandre Inacio Lemes ³Data  ³21/06/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relacao das movimentacoes internas                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ oExpO1: Objeto do relatorio                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportDef()

Local aOrdem   := {STR0004,STR0005,STR0006,STR0007,STR0008,STR0009,STR0010,STR0011} //" Codigo Produto "###" Ordem Producao "###" Classificacao  "###" Tipo Produto   "###" Grupo Produto  "###" Centro Custo   "###" Conta Contabil "###" Nr. Documento  "
Local cTitle   := STR0001 //"Relacao das Movimentacoes Internas"
Local oReport 
Local oSection1
Local oSection2
Local lVersao := (VAL(GetVersao(.F.)) == 11 .And. GetRpoRelease() >= "R6" .Or. VAL(GetVersao(.F.)) > 11)

#IFDEF TOP
	Local cAliasSD3 := GetNextAlias()
#ELSE
	Local cAliasSD3 := "SD3"
#ENDIF

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                         ³
//³ mv_par01     // Produto inicial                              ³
//³ mv_par02     // Produto final                                ³
//³ mv_par03     // Ordem de producao inicial                    ³
//³ mv_par04     // Ordem de producao final                      ³
//³ mv_par05     // classificacao do movimento inicial           ³
//³ mv_par06     // classificacao do movimento final             ³
//³ mv_par07     // tipo do produto inicial                      ³
//³ mv_par08     // tipo do produto final                        ³
//³ mv_par09     // grupo do produto inicial                     ³
//³ mv_par10     // grupo do produto final                       ³
//³ mv_par11     // centro de custo do produto inicial           ³
//³ mv_par12     // centro de custo do produto final             ³
//³ mv_par13     // conta contabil do produto inicial            ³
//³ mv_par14     // conta contabil do produto final              ³
//³ mv_par15     // moeda selecionada ( 1 a 5 )                  ³
//³ mv_par16     // Data inicial                                 ³
//³ mv_par17     // Data final                                   ³
//³ mv_par18     // 1 - Analitico , 2 - Sintetico                ³
//³ mv_par19     // De  Local                                    ³
//³ mv_par20     // Ate Local                                    ³
//³ mv_par21     // De  Documento                                ³
//³ mv_par22     // Ate Documento                                ³
//³ mv_par23     // De  Lote                                     ³
//³ mv_par24     // Ate Lote                                     ³
//³ mv_par25     // De  SubLote                                  ³
//³ mv_par26     // Ate SubLote                                  ³
//³ mv_par27     // Imprime a Descricao do Armazem               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AjustaSX1()
Pergunte("MTR300",.F.)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao do componente de impressao                                      ³
//³                                                                        ³
//³TReport():New                                                           ³
//³ExpC1 : Nome do relatorio                                               ³
//³ExpC2 : Titulo                                                          ³
//³ExpC3 : Pergunte                                                        ³
//³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
//³ExpC5 : Descricao                                                       ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oReport:= TReport():New("MTR300",cTitle,"MTR300", {|oReport| ReportPrint(oReport,aOrdem,cAliasSD3)},STR0002+" "+STR0003) //"Lista  as  movimentacoes  internas  da empresa  ,ou seja ,Requisicoes ,""Devolucoes ,Producoes e Estornos de Producao."
oReport:SetTotalInLine(.F.)
oReport:SetLandscape() 
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao da secao utilizada pelo relatorio                               ³
//³                                                                        ³
//³TRSection():New                                                         ³
//³ExpO1 : Objeto TReport que a secao pertence                             ³
//³ExpC2 : Descricao da seçao                                              ³
//³ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   ³
//³        sera considerada como principal para a seção.                   ³
//³ExpA4 : Array com as Ordens do relatório                                ³
//³ExpL5 : Carrega campos do SX3 como celulas                              ³
//³        Default : False                                                 ³
//³ExpL6 : Carrega ordens do Sindex                                        ³
//³        Default : False                                                 ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao da celulas da secao do relatorio                                ³
//³                                                                        ³
//³TRCell():New                                                            ³
//³ExpO1 : Objeto TSection que a secao pertence                            ³
//³ExpC2 : Nome da celula do relatório. O SX3 será consultado              ³
//³ExpC3 : Nome da tabela de referencia da celula                          ³
//³ExpC4 : Titulo da celula                                                ³
//³        Default : X3Titulo()                                            ³
//³ExpC5 : Picture                                                         ³
//³        Default : X3_PICTURE                                            ³
//³ExpC6 : Tamanho                                                         ³
//³        Default : X3_TAMANHO                                            ³
//³ExpL7 : Informe se o tamanho esta em pixel                              ³
//³        Default : False                                                 ³
//³ExpB8 : Bloco de código para impressao.                                 ³
//³        Default : ExpC2                                                 ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSection1:= TRSection():New(oReport,STR0051,{"SD3"},aOrdem) // "Produtos"
oSection1:SetTotalInLine(.F.)
oSection1:SetLineStyle()

TRCell():New(oSection1,"D3_COD"    ,"SD3",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"B1_DESC"   ,"SB1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"B1_TIPO"   ,"SB1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"D3_GRUPO"  ,"SD3",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"B1_UM"     ,"SB1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"  "        ,"SB1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)

oSection2:= TRSection():New(oSection1,STR0052,{"SD3"}) //"Itens de Movimentação Interna" 
oSection2:SetTotalInLine(.F.)
oSection2:SetHeaderPage()

TRCell():New(oSection2,"D3_LOCAL"  ,"SD3",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
If !lVersao
	TRCell():New(oSection2,"B2_LOCALIZ" ,"SB2"," "+STR0042,/*Picture*/,/*Tamanho*/,/*lPixel*/,{|| Posicione("SB2",1,xFilial("SB2")+((cAliasSD3)->D3_COD+(cAliasSD3)->D3_LOCAL),"B2_LOCALIZ")})
Else 																										
	TRCell():New(oSection2,"NNR_DESCRI","NNR",STR0042,/*Picture*/,/*Tamanho*/,/*lPixel*/,{|| Posicione("NNR",1,xFilial("NNR")+((cAliasSD3)->D3_LOCAL),"NNR_DESCRI")})
EndIf
 
TRCell():New(oSection2,"QTDENTRADA","   ",STR0043,PesqPictQt("D3_QUANT",17),TamSX3("D3_QUANT")[1],/*lPixel*/,{|| If((cAliasSD3)->D3_TM <= "500" ,(cAliasSD3)->D3_QUANT, 0 ) })
TRCell():New(oSection2,"QTDSAIDA"  ,"   ",STR0044,PesqPictQt("D3_QUANT",17),TamSX3("D3_QUANT")[1],/*lPixel*/,{|| If((cAliasSD3)->D3_TM > "500",(cAliasSD3)->D3_QUANT, 0 ) })
TRCell():New(oSection2,"CUSTOUNIT" ,"   ",STR0045,PesqPict("SD3", "D3_CUSTO"+Str(mv_par15,1),17),/*Tamanho*/,/*lPixel*/,{|| (cAliasSD3)->&("D3_CUSTO"+Str(mv_par15,1)) / If((cAliasSD3)->D3_QUANT == 0,1,(cAliasSD3)->D3_QUANT ) })
TRCell():New(oSection2,"CUSTO"     ,"   ",STR0046,PesqPict("SD3", "D3_CUSTO"+Str(mv_par15,1),17),/*Tamanho*/,/*lPixel*/,{||  If((cAliasSD3)->D3_TM > "500",((cAliasSD3)->&("D3_CUSTO"+Str(mv_par15,1)))* -1,(cAliasSD3)->&("D3_CUSTO"+Str(mv_par15,1))) })
TRCell():New(oSection2,"D3_TM"     ,"SD3",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection2,"D3_CF"     ,"SD3",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection2,"D3_OP"     ,"SD3",STR0047,"@!",6,/*lPixel*/,{|| SubStr(D3_OP,1,6) }) //"Ordem Producao"
TRCell():New(oSection2,"D3_OP"     ,"SD3",STR0048,"@!",2,/*lPixel*/,{|| SubStr(D3_OP,7,2) })
TRCell():New(oSection2,"D3_OP"     ,"SD3",STR0049,"@!",3,/*lPixel*/,{|| SubStr(D3_OP,9,3) })
TRCell():New(oSection2,"D3_NUMSEQ" ,"SD3",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection2,"D3_CC"     ,"SD3",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection2,"D3_CONTA"  ,"SD3",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection2,"D3_DOC"    ,"SD3",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection2,"D3_EMISSAO","SD3",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection2,"D3_USUARIO","SD3",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)

Return(oReport)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ReportPrin³ Autor ³Alexandre Inacio Lemes ³Data  ³21/06/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relacao das movimentacoes internas                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpO1: Objeto Report do Relatório                           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportPrint(oReport,aOrdem,cAliasSD3)

Local oSection1  := oReport:Section(1) 
Local oSection2  := oReport:Section(1):Section(1)  
Local nOrdem     := oReport:Section(1):GetOrder()
Local aFileCtb   := If( CtbInUse() , { "CT1", "CTT" } , { "SI1", "SI3" } )
Local cCposCtb   := If(aFileCtb[1] = "CT1", If( nOrdem ==6 , "CTT->CTT_CUSTO + '-' + CTT->CTT_DESC" + StrZero(mv_par15, 2) , ;
                  "CT1->CT1_DESC" + StrZero(mv_par15, 2)) , If( nOrdem ==6 , "SI3->I3_CUSTO + '-' + SI3->I3_DESC", "SI1->I1_DESC"))
Local cPicD3Cust := PesqPict("SD3", "D3_CUSTO"+Str(mv_par15,1),17)
Local cChave	 := ""
Local cCodPro	 := ""
Local cKeyForTot := ""
Local cSubTotal  := ""
Local cTotaliza  := ""
Local cSubCtb    := ""
Local nProdSegEnt:= 0
Local nProdSegSai:= 0
Local nGSegEnt	 := 0
Local nGSegSai   := 0
Local nTotSegEnt := 0
Local nTotSegSai := 0
Local cWhereD3   := '%%'        
Local lVersao    := (VAL(GetVersao(.F.)) == 11 .And. GetRpoRelease() >= "R6" .Or. VAL(GetVersao(.F.)) > 11)
Local lPlanilha  := oReport:nDevice == 4 // planilha
Local oSection3
	
#IFDEF TOP
	Local cOrder    := ""
#ELSE
	Local cCondicao := ""
	Local cIndexKey := ""
#ENDIF

If lPlanilha
	oSection3  := oReport:Section(2)

	oSection3:= TRSection():New(oReport,"",{"SD3"}) //"Itens de Movimentação Interna" 
	oSection3:SetTotalInLine(.F.)
	oSection3:lPrintHeader := .F.
	oSection3:lHeaderVisible := .F.
	
	TRCell():New(oSection3,"D3_LOCAL"  ,"SD3",/*Titulo*/,/*Picture*/									 , /*Tamanho*/		   ,/*lPixel*/ ,/*{|| code-block de impressao }*/)
	TRCell():New(oSection3,"QTDENTRADA","   ",""		 ,PesqPictQt("D3_QUANT",17)						 ,TamSX3("D3_QUANT")[1],/*lPixel*/,{|| If((cAliasSD3)->D3_TM <= "500" ,(cAliasSD3)->D3_QUANT, 0) })
	TRCell():New(oSection3,"QTDSAIDA"  ,"   ",""		 ,PesqPictQt("D3_QUANT",17)						 ,TamSX3("D3_QUANT")[1],/*lPixel*/,{|| If((cAliasSD3)->D3_TM > "500",(cAliasSD3)->D3_QUANT, 0 ) })
	TRCell():New(oSection3,"CUSTOUNIT" ,"   ",""		 ,PesqPict("SD3", "D3_CUSTO"+Str(mv_par15,1),17),TamSX3("D3_QUANT")[1],/*lPixel*/,{|| (cAliasSD3)->&("D3_CUSTO"+Str(mv_par15,1)) / If((cAliasSD3)->D3_QUANT == 0,1,(cAliasSD3)->D3_QUANT ) })
	TRCell():New(oSection3,"CUSTO"     ,"   ",""		 ,PesqPict("SD3", "D3_CUSTO"+Str(mv_par15,1),17),TamSX3("D3_QUANT")[1],/*lPixel*/,{||  If((cAliasSD3)->D3_TM > "500",((cAliasSD3)->&("D3_CUSTO"+Str(mv_par15,1)))* -1,(cAliasSD3)->&("D3_CUSTO"+Str(mv_par15,1))) })
Else
	oSection2:SetTotalText("Total Produto") //"Total Produto"  
	TRFunction():New(oSection2:Cell("QTDENTRADA"),NIL,"SUM",/*oBreak1*/,,/*cPicture*/,/*uFormula*/,.T.,.T.,,oSection1)
	TRFunction():New(oSection2:Cell("QTDSAIDA"  ),NIL,"SUM",/*oBreak1*/,,/*cPicture*/,/*uFormula*/,.T.,.T.,,oSection1) 
	TRFunction():New(oSection2:Cell("CUSTO"     ),NIL,"SUM",/*oBreak1*/,,/*cPicture*/,/*uFormula*/,.T.,.T.,,oSection1) 
EndIf	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Adiciona a ordem escolhida ao titulo do relatorio          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oReport:SetTitle(oReport:Title() + STR0040+AllTrim(aOrdem[nOrdem])+STR0041+AllTrim(GetMv("MV_SIMB"+Ltrim(Str(mv_par15))))+")" )//" (Por "###" ,em " 

dbSelectArea("SD3")
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Filtragem do relatório                                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
#IFDEF TOP
	
 	MakeSqlExpr(oReport:uParam)
    
 	oReport:Section(1):BeginQuery()	
 	
	If ! __lPyme 
		cWhereD3 := "%" 
		cWhereD3 += "D3_LOTECTL >= '"+mv_par23+"' AND "
		cWhereD3 += "D3_LOTECTL <= '"+mv_par24+"' AND "     
		cWhereD3 += "D3_NUMLOTE >= '"+mv_par25+"' AND "
		cWhereD3 += "D3_NUMLOTE <= '"+mv_par26+"' AND "     
		cWhereD3 += "%" 
	Endif 	
 
	Do Case
		Case nOrdem == 1
			cOrder := "% D3_FILIAL,D3_COD,D3_LOCAL,D3_NUMSEQ %"
		Case nOrdem == 2
			cOrder := "% D3_FILIAL,D3_OP,D3_COD,D3_LOCAL %"
		Case nOrdem == 3
			cOrder := "% D3_FILIAL,D3_CF,D3_COD,D3_LOCAL %"
		Case nOrdem == 4
			cOrder := "% D3_FILIAL,D3_TIPO,D3_COD,D3_LOCAL %"
		Case nOrdem == 5
			cOrder := "% D3_FILIAL,D3_GRUPO,D3_COD,D3_LOCAL %"
		Case nOrdem == 6
			cOrder := "% D3_FILIAL,D3_CC,D3_COD,D3_LOCAL %"
		Case nOrdem == 7
			cOrder := "% D3_FILIAL,D3_CONTA,D3_COD,D3_LOCAL %"
		Case nOrdem == 8
			cOrder := "% D3_FILIAL,D3_DOC,D3_COD %"
	EndCase
				

	BeginSql Alias cAliasSD3
	 
	SELECT SD3.*
	FROM %table:SD3% SD3
	 
	WHERE SD3.D3_FILIAL  = %xFilial:SD3% AND 
		SD3.D3_COD      >= %Exp:mv_par01% AND 
	    SD3.D3_COD      <= %Exp:mv_par02% AND      
	    SD3.D3_OP       >= %Exp:mv_par03% AND 
	    SD3.D3_OP       <= %Exp:mv_par04% AND          
	    SD3.D3_CF       >= %Exp:mv_par05% AND 
	    SD3.D3_CF       <= %Exp:mv_par06% AND      
	    SD3.D3_TIPO     >= %Exp:mv_par07% AND 
	    SD3.D3_TIPO     <= %Exp:mv_par08% AND      
	    SD3.D3_GRUPO    >= %Exp:mv_par09% AND 
	    SD3.D3_GRUPO    <= %Exp:mv_par10% AND          
	    SD3.D3_CC       >= %Exp:mv_par11% AND 
	    SD3.D3_CC       <= %Exp:mv_par12% AND          
	    SD3.D3_CONTA    >= %Exp:mv_par13% AND 
	    SD3.D3_CONTA    <= %Exp:mv_par14% AND      
	    SD3.D3_EMISSAO  >= %Exp:Dtos(mv_par16)% AND 
	    SD3.D3_EMISSAO  <= %Exp:Dtos(mv_par17)% AND 
	    SD3.D3_LOCAL    >= %Exp:mv_par19% AND 
	    SD3.D3_LOCAL    <= %Exp:mv_par20% AND          
	    %Exp:cWhereD3%
	    SD3.%NotDel% 
	    
	ORDER BY %Exp:cOrder%
	   
	EndSql
	
	oReport:Section(1):EndQuery(/*Array com os parametros do tipo Range*/)
	If lPlanilha
		oSection3:SetParentQuery()
	EndIf	
	oSection2:SetParentQuery()

#ELSE

	MakeAdvplExpr(oReport:uParam)

	cCondicao := 'D3_FILIAL=="' + xFilial()+ '".And.'
	cCondicao += 'D3_COD>="'    + mv_par01 + '".And.D3_COD<="'    + mv_par02 + '".And.'
	cCondicao += 'D3_LOCAL>="'  + mv_par19 + '".And.D3_LOCAL<="'  + mv_par20 + '".And.'
	cCondicao += 'D3_OP>="'     + mv_par03 + '".And.D3_OP<="'     + mv_par04 + '".And.'
	cCondicao += 'D3_CF>="'     + mv_par05 + '".And.D3_CF<="'     + mv_par06 + '".And.'
	cCondicao += 'D3_TIPO>="'   + mv_par07 + '".And.D3_TIPO<="'   + mv_par08 + '".And.'
	If ! __lPyme 
		cCondicao += 'D3_LOTECTL>="'+ mv_par23 + '".And.D3_LOTECTL<="'+ mv_par24 + '".And.'
		cCondicao += 'D3_NUMLOTE>="'+ mv_par25 + '".And.D3_NUMLOTE<="'+ mv_par26 + '".And.'
	Endif	
	cCondicao += 'D3_GRUPO>="'  + mv_par09 + '".And.D3_GRUPO<="'  + mv_par10 + '".And.'
	cCondicao += 'D3_CC>="'     + mv_par11 + '".And.D3_CC<="'     + mv_par12 + '".And.'
	cCondicao += 'D3_CONTA>="'  + mv_par13 + '".And.D3_CONTA<="'  + mv_par14 + '".And.'
	cCondicao += 'DTOS(D3_EMISSAO)>="'+DTOS(mv_par16)+'".And.DTOS(D3_EMISSAO)<="'+DTOS(mv_par17)+'"'

	dbSelectArea("SD3")
	Do Case
		Case nOrdem == 1
			SD3->(dbSetOrder(3))
			cIndexKey := IndexKey()
		Case nOrdem == 2
			SD3->(dbSetOrder(1))
			cIndexKey := IndexKey()
		Case nOrdem == 3
			cIndexKey := "D3_FILIAL+D3_CF+D3_COD+D3_LOCAL"
		Case nOrdem == 4
			cIndexKey := "D3_FILIAL+D3_TIPO+D3_COD+D3_LOCAL"
		Case nOrdem == 5
			cIndexKey := "D3_FILIAL+D3_GRUPO+D3_COD+D3_LOCAL"
		Case nOrdem == 6
			cIndexKey := "D3_FILIAL+D3_CC+D3_COD+D3_LOCAL"
		Case nOrdem == 7
			cIndexKey := "D3_FILIAL+D3_CONTA+D3_COD+D3_LOCAL"
		Case nOrdem == 8
			SD3->(dbSetOrder(2))
			cIndexKey := IndexKey()
	EndCase
			
	oReport:Section(1):SetFilter(cCondicao,cIndexKey)

#ENDIF		
	
If lPlanilha
	oSection3:Cell("QTDENTRADA"):Hide()
	oSection3:Cell("QTDSAIDA"):Hide()
	oSection3:Cell("CUSTO"):Hide()                                 
	oSection3:Cell("CUSTOUNIT"):Hide()
	oSection3:Cell("D3_LOCAL"):Hide()
EndIf

If mv_par18 == 2
	oSection2:Hide()
EndIf

If mv_par27 == 2
	 If lVersao
	 	oSection2:Cell("NNR_DESCRI"):Disable()
	 Else
	 	oSection2:Cell("B2_LOCALIZ"):Disable()
	 EndIf
EndIf

Do Case
	Case nOrdem == 1
		cKeyForTot := "D3_COD"
		cSubTotal  := STR0017 //"......... Total do Produto "
	Case nOrdem == 2
		cKeyForTot := "D3_OP"
		cSubTotal  := STR0018 //"......... Total da Ordem de Producao "
	Case nOrdem == 3
		cKeyForTot := "D3_CF"
		cSubTotal  := STR0020 //"......... Total da Classificacao "
	Case nOrdem == 4
		cKeyForTot := "D3_TIPO"
		cSubTotal  := STR0021 //"......... Total do Tipo "
	Case nOrdem == 5
		cKeyForTot := "D3_GRUPO"
		cSubTotal  := STR0022 //"......... Total do Grupo "
	Case nOrdem == 6
		cKeyForTot := "D3_CC"
		cSubTotal  := STR0023 //"......... Total do Centro de Custo"
	Case nOrdem == 7
		cKeyForTot := "D3_CONTA"
		cSubTotal  := STR0024 //"......... Total da Conta"
	Case nOrdem == 8
		cKeyForTot := "D3_DOC"
		cSubTotal  := STR0025 //"......... Total do Documento "
EndCase

If lPlanilha
	oSection3:SetTotalText("Total Produto") //"Total Produto"  
	TRFunction():New(oSection3:Cell("QTDENTRADA"),NIL,"SUM",/*oBreak1*/,,/*cPicture*/,/*uFormula*/,.T.,.T.,,oSection2)
	TRFunction():New(oSection3:Cell("QTDSAIDA"  ),NIL,"SUM",/*oBreak1*/,,/*cPicture*/,/*uFormula*/,.T.,.T.,,oSection2) 
	TRFunction():New(oSection3:Cell("CUSTO"     ),NIL,"SUM",/*oBreak1*/,,/*cPicture*/,/*uFormula*/,.T.,.T.,,oSection2) 
EndIf

TRPosition():New(oSection1,"SB1",1,{|| xFilial("SB1") + (cAliasSD3)->D3_COD })
TRPosition():New(oSection1,"SB2",1,{|| xFilial("SB2") + (cAliasSD3)->D3_COD + (cAliasSD3)->D3_LOCAL })

oReport:SetPageFooter(3, {|| f_Footer(oReport, oSection2)}, .F.)

oReport:SetMeter(SD3->(LastRec()))
dbSelectArea(cAliasSD3)

While !oReport:Cancel() .And. !(cAliasSD3)->(Eof())

	oReport:IncMeter()
	If oReport:Cancel()
		Exit
	EndIf
	
	If UPPER((cAliasSD3)->D3_DOC) < UPPER(mv_par21) .Or. UPPER((cAliasSD3)->D3_DOC) > UPPER(mv_par22)
		(cAliasSD3)->(dbSkip())
		Loop
	EndIf
	If !D3Valido(cAliasSD3)
		(cAliasSD3)->(dbSkip())
		Loop
	EndIf  
	
 	oSection1:Init()
 	oSection1:PrintLine()		

	nProdSegEnt := 0
	nProdSegSai := 0
	
	cChave := (cAliasSD3)->&(cKeyForTot) 
	cCodPro:= (cAliasSD3)->D3_COD
	
	If lPlanilha
		oSection3:Init(.F.)
	EndIf
	
 	oSection2:Init()
	While !oReport:Cancel() .And. !(cAliasSD3)->(Eof()) .And. cChave == (cAliasSD3)->&(cKeyForTot) .And. cCodPro == (cAliasSD3)->D3_COD

		oReport:IncMeter()
		If oReport:Cancel()
			Exit
		EndIf
		
		If UPPER((cAliasSD3)->D3_DOC) < UPPER(mv_par21) .Or. UPPER((cAliasSD3)->D3_DOC) > UPPER(mv_par22)
			(cAliasSD3)->(dbSkip())
			Loop
		EndIf		
	
		If !D3Valido(cAliasSD3)
			(cAliasSD3)->(dbSkip())
			Loop
		EndIf
	
  		cTotaliza := (cAliasSD3)->&(cKeyForTot)
		
		If lPlanilha
			oSection3:PrintLine()
		EndIf
			  		          
   		oSection2:PrintLine()

		
		If (cAliasSD3)->D3_TM <= "500"
			nProdSegEnt += If(SB1->B1_CONV>0,ConvUm((cAliasSD3)->D3_COD,(cAliasSD3)->D3_QUANT,0,2),(cAliasSD3)->D3_QTSEGUM )
        Else
			nProdSegSai += If(SB1->B1_CONV>0,ConvUm((cAliasSD3)->D3_COD,(cAliasSD3)->D3_QUANT,0,2),(cAliasSD3)->D3_QTSEGUM )	
        EndIf
        
		cSubCtb := ""
		If nOrdem == 6 .Or. nOrdem == 7
			If nOrdem == 6 .And. (aFileCtb[2])->(dbSeek(xFilial(aFileCtb[2])+(cAliasSD3)->D3_CC,   .F.)) .Or. ;
			   nOrdem == 7 .And. (aFileCtb[1])->(dbSeek(xFilial(aFileCtb[1])+(cAliasSD3)->D3_CONTA,.F.))
			   cSubCtb := &(cCposCtb)
			ElseIf !Empty((cAliasSD3)->D3_CC) .Or. !Empty((cAliasSD3)->D3_CONTA)
			   cSubCtb := STR0037 // ' ** CC nao cadastrado ** '
			EndIf
		EndIf

		dbSelectArea(cAliasSD3)
		dbSkip()
			
	EndDo

	oReport:PrintText(STR0033+SB1->B1_SEGUM+ STR0034+TransForm(nProdSegEnt,cPicD3Cust)+"   "+;
	STR0035+TransForm(nProdSegSai,cPicD3Cust),,oSection1:Cell("B1_UM"):ColPos()) //"Quantidade na Seg. UM ("###"): Entrada -> "
	

	oSection1:Finish()
	nGSegEnt += nProdSegEnt 
	nGSegSai += nProdSegSai
	nTotSegEnt += nProdSegEnt 
	nTotSegSai += nProdSegSai

	If cTotaliza <> (cAliasSD3)->&(cKeyForTot) .Or. (cAliasSD3)->(Eof())

		oSection2 :SetTotalText( cSubTotal + cTotaliza + cSubCtb ) //" Subtotal das Ordens do Relatorio

		If (cAliasSD3)->(Eof())
			If nOrdem ==1
				nProdSegEnt:=nGSegEnt
				nProdSegSai:=nGSegSai
			Else
				nTotSegEnt:=nGSegEnt
				nTotSegSai:=nGSegSai
			EndIf
		EndIf
		
		If lPlanilha
			oSection3:Finish()
		EndIf
			
  		oSection2:Finish()
		If (cAliasSD3)->(Eof())
			oReport:PrintText(PadR(STR0055,Len(STR0033)+2+TamSx3('B1_SEGUM')[1])+Substr(STR0034,3)+TransForm(nGSegEnt,cPicD3Cust)+"   "+;
			STR0035+TransForm(nGSegSai,cPicD3Cust),oReport:Row()+2,oSection1:Cell("B1_UM"):ColPos())

		ElseIf nOrdem <> 1 .And. cTotaliza <> (cAliasSD3)->&(cKeyForTot)
			oReport:PrintText(Left(STR0033,Len(STR0033)-2)+Space(2+TamSx3('B1_SEGUM')[1])+"  "+Substr(STR0034,3)+TransForm(nTotSegEnt,cPicD3Cust)+"   "+;
			STR0035+TransForm(nTotSegSai,cPicD3Cust),,oSection1:Cell("B1_UM"):ColPos())
			nTotSegEnt:=0
			nTotSegSai:=0
		EndIf

  	EndIf
	
EndDo

Return Nil 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MATR300R3³ Autor ³ Eveli Morasco         ³ Data ³ 02/03/93 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relacao das movimentacoes internas                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Marcelo Pim.³06/02/98³12531A³Ajuste do total geral na 2a UM.           ³±±
±±³Eduardo     ³09/06/98³XXXXXX³Ajuste da variavel cmens linha 429        ³±±
±±³Rodrigo     ³23/06/98³XXXXXX³Acerto no tamanho do documento para 12    ³±±
±±³            ³        ³      ³posicoes                                  ³±±
±±³FernandoJoly³16/09/98³17338A³Ajuste da variavel cmens - a descri‡„o do ³±±
±±³            ³        ³      ³CC devera corresponder ao CC correto.     ³±±
±±³FernandoJoly³10/11/98³XXXXXX³Ajuste para o Ano 2000.                   ³±±
±±³CesarValadao³19/03/99³20515A³Corre‡„o de totaliza‡„o por Centro Custo  ³±±
±±³CesarValadao³30/03/99³XXXXXX³Manutencao na SetPrint()                  ³±±
±±³CesarValadao³29/04/99³21555A³Manutencao Lay-Out P Imprimir Cpos Maiores³±±
±±³CesarValadao³14/07/99³22097A³Acerto no Cabecalho, Custos e Totais.     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function MATR300R3
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis tipo Local padrao de todos os relatorios           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
LOCAL Tamanho  := "G"
LOCAL titulo   := STR0001	//"Relacao das Movimentacoes Internas"
LOCAL cDesc1   := STR0002	//"Lista  as  movimentacoes  internas  da empresa  ,ou seja ,Requisicoes ,"
LOCAL cDesc2   := STR0003	//"Devolucoes ,Producoes e Estornos de Producao."
LOCAL cDesc3   := ""
LOCAL cString  := "SD3"
LOCAL nTipo    := 0
LOCAL aOrd     := {OemToAnsi(STR0004),OemToAnsi(STR0005),OemToAnsi(STR0006),OemToAnsi(STR0007),OemToAnsi(STR0008),OemToAnsi(STR0009),OemToAnsi(STR0010),OemToAnsi(STR0011)}			//" Codigo Produto "###" Ordem Producao "###" Classificacao  "###" Tipo Produto   "###" Grupo Produto  "###" Centro Custo   "###" Conta Contabil "###" Nr. Documento  "
LOCAL wnrel := "MATR300"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis privadas ,padrao de todos os relatorios            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE aReturn := {OemToAnsi(STR0012), 1,OemToAnsi(STR0013), 2, 2, 1, "",1 }		//"Zebrado"###"Administracao"
PRIVATE nLastKey := 0 ,cPerg := "MTR300"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AjustaSX1()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                         ³
//³ mv_par01     // Produto inicial                              ³
//³ mv_par02     // Produto final                                ³
//³ mv_par03     // Ordem de producao inicial                    ³
//³ mv_par04     // Ordem de producao final                      ³
//³ mv_par05     // classificacao do movimento inicial           ³
//³ mv_par06     // classificacao do movimento final             ³
//³ mv_par07     // tipo do produto inicial                      ³
//³ mv_par08     // tipo do produto final                        ³
//³ mv_par09     // grupo do produto inicial                     ³
//³ mv_par10     // grupo do produto final                       ³
//³ mv_par11     // centro de custo do produto inicial           ³
//³ mv_par12     // centro de custo do produto final             ³
//³ mv_par13     // conta contabil do produto inicial            ³
//³ mv_par14     // conta contabil do produto final              ³
//³ mv_par15     // moeda selecionada ( 1 a 5 )                  ³
//³ mv_par16     // Data inicial                                 ³
//³ mv_par17     // Data final                                   ³
//³ mv_par18     // 1 - Analitico , 2 - Sintetico                ³
//³ mv_par19     // De  Local                                    ³
//³ mv_par20     // Ate Local                                    ³
//³ mv_par21     // De  Documento                                ³
//³ mv_par22     // Ate Documento                                ³
//³ mv_par23     // De  Lote                                     ³
//³ mv_par24     // Ate Lote                                     ³
//³ mv_par25     // De  SubLote                                  ³
//³ mv_par26     // Ate SubLote                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
pergunte(cPerg,.F.)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia controle para a funcao SETPRINT                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
wnrel := SetPrint(cString,wnrel,cPerg,titulo,cDesc1,cDesc2,cDesc3,.F.,aOrd,.T.,Tamanho)

If nLastKey = 27
	dbClearFilter()
	Return
Endif

SetDefault(aReturn,cString)

If nLastKey = 27
	dbClearFilter()
	Return
Endif

RptStatus({|lEnd| C300Imp(aOrd,@lEnd,wnRel,cString,tamanho,titulo)},titulo)

Return NIL

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ C300IMP  ³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 12.12.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Chamada do Relatorio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR300                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function C300Imp(aOrd,lEnd,WnRel,cString,tamanho,titulo)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis locais especificas deste relatorio                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
LOCAL cRodaTxt 	:= STR0014		//"MOVIMENTO(S)"
LOCAL nCntImpr 	:= 0
LOCAL nProdEnt,nProdSai,nProdVal,nQuebraEnt,nQuebraSai,nQuebraVal,nGeralEnt,nGeralSai,nGeralVal,nQSegEnt,nQSegSai,nGSegEnt,nGSegSai
LOCAL cCampoCus  ,lPassou1  ,nCntProd   ,cCompCampo ,lImprime  ,cCodAnt
LOCAL nCusto ,nCM, cDet := ''
LOCAL nIndex ,cSeek  
Local aEntCt    := If(CtbInUse(), { "CT1", "CTT" }, { "SI1", "SI3" })
Local cEntCpo 	:= If(aEntCt[1] = "CT1", If(aReturn[8]==6,;
					"CTT->CTT_CUSTO + '-' + CTT->CTT_DESC" + StrZero(mv_par15, 2),;
					"CT1->CT1_DESC" + StrZero(mv_par15, 2)),;
					If(aReturn[8]==6,;
					"SI3->I3_CUSTO + '-' + SI3->I3_DESC", "SI1->I1_DESC"))
Local lDescArm  := .F. 
Local nTamDoc   := TamSX3("D3_DOC")[1]
Local lVersao   := (VAL(GetVersao(.F.)) == 11 .And. GetRpoRelease() >= "R6" .Or. VAL(GetVersao(.F.)) > 11)
Local cLocaliz  := "" 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis privadas especificas deste relatorio               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE cCondicao ,cCondSec ,cNomArq ,lContinua

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Contadores de linha e pagina                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE li := 80 ,m_pag := 1

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se deve comprimir ou nao                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nTipo  := IIF(aReturn[4]==1,15,18)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Adiciona a ordem escolhida ao titulo do relatorio          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Type("NewHead")#"U"
	NewHead += STR0040+AllTrim(aOrd[aReturn[8]])+STR0041+AllTrim(GetMv("MV_SIMB"+Ltrim(Str(mv_par15))))+")" //" (Por "###" ,em "
Else
	Titulo  += STR0040+AllTrim(aOrd[aReturn[8]])+STR0041+AllTrim(GetMv("MV_SIMB"+Ltrim(Str(mv_par15))))+")" //" (Por "###" ,em "
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta o cabecalho do relatorio                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//"ALMOXARIFADO                    QUANTIDADE                            CUSTO                 CUSTO    TIPO DE     TIPO    ORDEM DE IT SEQ    CENTRO              CONTA           DOCUMENTO       EMISSAO      NOME DO"
//"                      ENTRADA                   SAIDA              UNITARIO                 TOTAL    MOVIMENTO   RE/DE   PRODUCAO           CUSTO              CONTABIL                                      USUARIO"
// XX            99,999,999,999.99       99,999,999,999.99         99,999,999,999.99     99,999,999,999.99       123        123     123456   12 123    123456789    123456789012345   123456        12/12/12    XXXXXXXXXXXXXXX
// 0         1         2         3         4         5         6         7         8         9        10        11        12        13        14        15        16        17        18        19        20        21        22
// 01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
If nTamDoc > 13 
	Cabec1 := STR0015+If(mv_par18==1, STR0053, "")
	Cabec2 := STR0016+If(mv_par18==1, STR0054, "")
Else
	Cabec1 := STR0015+If(mv_par18==1, STR0038, "")
	Cabec2 := STR0016+If(mv_par18==1, STR0039, "")
Endif

dbSelectArea("SD3")

cFiltro := "D3_FILIAL=='"+xFilial()+"'.And."
cFiltro += "D3_COD>='"+mv_par01+"'.And.D3_COD<='"+mv_par02+"'.And."
cFiltro += "D3_LOCAL>='"+mv_par19+"'.And.D3_LOCAL<='"+mv_par20+"'.And."
cFiltro += "D3_OP>='"+mv_par03+"'.And.D3_OP<='"+mv_par04+"'.And."
cFiltro += "D3_CF>='"+ mv_par05+"'.And.D3_CF<='"+mv_par06+"'.And."
If ! __lPyme 
	cFiltro += "D3_LOTECTL>='"+mv_par23+"'.And. D3_LOTECTL<='"+mv_par24+"'.And."
	cFiltro += "D3_NUMLOTE>='"+mv_par25+"'.And. D3_NUMLOTE<='"+mv_par26+"'.And."
Endif	
cFiltro += "D3_TIPO>='"+mv_par07+"'.And. D3_TIPO<='"+mv_par08+"'"
#IFDEF TOP
	cFiltro += ".And.D3_GRUPO>='"+mv_par09+"'.And.D3_GRUPO<='"+mv_par10+"'.And."
	cFiltro += "D3_CC>='"+mv_par11+"'.And.D3_CC<='"+mv_par12+"'.And."
	cFiltro += "D3_CONTA>='"+mv_par13+"'.And.D3_CONTA<='"+mv_par14+"'.And."
	cFiltro += "DTOS(D3_EMISSAO)>='"+DTOS(mv_par16)+"'.And.DTOS(D3_EMISSAO)<='"+DTOS(mv_par17)+"'"
#ENDIF	

Do Case
	Case aReturn[8] == 1
		cNomArq := CriaTrab("",.F.)
		Set Order To 3
		IndRegua("SD3",cNomArq,IndexKey(),,cFiltro,STR0019) 		//"Selecionando Registros..."
		cSeek :=cFilial+mv_par01
		cCondicao  := "lContinua .And. !EOF() .And. D3_COD <= mv_par02"
		cCondSec   := "D3_COD == cVar"
		cCompCampo := "D3_COD"
		cMens      := STR0017 //"......... Total do Produto "
	Case aReturn[8] == 2
		cNomArq := CriaTrab("",.F.)
		Set Order To 1
		IndRegua("SD3",cNomArq,IndexKey(),,cFiltro,STR0019) 		//"Selecionando Registros..."
		cSeek :=cFilial+mv_par03
		cCondicao  := "lContinua .And. !EOF() .And. D3_OP <= mv_par04"
		cCondSec   := "D3_OP == cVar"
		cCompCampo := "D3_OP"
		cMens      := STR0018 //"......... Total da Ordem de Producao "
	Case aReturn[8] == 3
		cNomArq := CriaTrab("",.F.)
		IndRegua("SD3",cNomArq,"D3_FILIAL+D3_CF+D3_COD+D3_LOCAL",,cFiltro,STR0019)			//"Selecionando Registros..."
		cSeek :=cFilial+mv_par05
		cCondicao  := "lContinua .And. !EOF() .And. D3_CF <= mv_par06"
		cCondSec   := "D3_CF == cVar"
		cCompCampo := "D3_CF"
		cMens      := STR0020 //"......... Total da Classificacao "
	Case aReturn[8] == 4
		cNomArq := CriaTrab("",.F.)
		IndRegua("SD3",cNomArq,"D3_FILIAL+D3_TIPO+D3_COD+D3_LOCAL",,cFiltro,STR0019)		//"Selecionando Registros..."
		cSeek := cFilial+mv_par07
		cCondicao  := "lContinua .And. !EOF() .And. D3_TIPO <= mv_par08"
		cCondSec   := "D3_TIPO == cVar"
		cCompCampo := "D3_TIPO"
		cMens      := STR0021 //"......... Total do Tipo "
	Case aReturn[8] == 5
		cNomArq := CriaTrab("",.F.)
		IndRegua("SD3",cNomArq,"D3_FILIAL+D3_GRUPO+D3_COD+D3_LOCAL",,cFiltro,STR0019)		//"Selecionando Registros..."
		cSeek := cFilial+mv_par09
		cCondicao  := "lContinua .And. !EOF() .And. D3_GRUPO <= mv_par10"
		cCondSec   := "D3_GRUPO == cVar"
		cCompCampo := "D3_GRUPO"
		cMens      := STR0022 //"......... Total do Grupo "
	Case aReturn[8] == 6
		cNomArq := CriaTrab("",.F.)
		IndRegua("SD3",cNomArq,"D3_FILIAL+D3_CC+D3_COD+D3_LOCAL",,cFiltro,STR0019)			//"Selecionando Registros..."
		cSeek :=cFilial+mv_par11
		cCondicao  := "lContinua .And. !EOF() .And. D3_CC <= mv_par12"
		cCondSec   := "D3_CC == cVar"
		cCompCampo := "D3_CC"
		cMens      := STR0023 //"......... Total do Centro de Custo"
	Case aReturn[8] == 7
		cNomArq := CriaTrab("",.F.)
		IndRegua("SD3",cNomArq,"D3_FILIAL+D3_CONTA+D3_COD+D3_LOCAL",,cFiltro,STR0019)		//"Selecionando Registros..."
		cSeek :=cFilial+mv_par13
		cCondicao  := "lContinua .And. !EOF() .And. D3_CONTA <= mv_par14"
		cCondSec   := "D3_CONTA == cVar"
		cCompCampo := "D3_CONTA"
		cMens      := STR0024 //"......... Total da Conta"
	Case aReturn[8] == 8
		Set Order To 2
		cNomArq := CriaTrab("",.F.)
		IndRegua("SD3",cNomArq,IndexKey(),,cFiltro,STR0019)		//"Selecionando Registros..."
		cSeek :=cFilial
		cCondicao  := "lContinua .And. !EOF() "
		cCondSec   := "D3_DOC == cVar"
		cCompCampo := "D3_DOC"
		cMens      := STR0025 //"......... Total do Documento "
EndCase

nIndex := RetIndex("SD3")
#IFNDEF TOP
	dbSetIndex(cNomArq+OrdBagExt())
#ENDIF
dbSetOrder(nIndex+1)
SetRegua(RecCount())		// Total de Elementos da regua

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define o campo a ser impresso no valor de acordo com a moeda selecionada ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cCampoCus := "SD3->D3_CUSTO"+Str(mv_par15,1)

Store 0 To nGeralEnt,nGeralSai,nGeralVal,nGSegEnt,nGSegSai
lContinua := .T.
dbSeek(cSeek,.T.)
While &(cCondicao) .and. cFilial == D3_FILIAL
	
	cVar := &(cCompCampo)
	
	Store 0 To nQuebraEnt,nQuebraSai,nQuebraVal,nQSegEnt,nQSegSai
	lPassou1 := .F.
	
	If UPPER(D3_DOC) < UPPER(mv_par21) .Or. UPPER(D3_DOC) > UPPER(mv_par22)
		dbSkip()
		Loop
	EndIf
	While &cCondicao .And. &cCondSec .and. cFilial == D3_FILIAL
		
		cCodAnt := D3_COD
		Store 0 To nProdEnt,nProdSai,nProdVal,nProdSegEnt,nProdSegSai
		nCntProd := 0
		lImprime := .T.
		While &(cCondicao) .And. &(cCondSec) .And. D3_COD == cCodAnt .and. cFilial == D3_FILIAL
			
			If lEnd
				@ PROW()+1,001 PSay STR0026		//"CANCELADO PELO OPERADOR"
				lContinua := .F.
				Exit
			EndIf
			
			IncRegua()
			
			#IFNDEF TOP
				If D3_GRUPO < mv_par09 .Or. D3_GRUPO > mv_par10
					dbSkip()
					Loop
				EndIf
				
				If D3_CC < mv_par11 .Or. D3_CC > mv_par12
					dbSkip()
					Loop
				EndIf
				
				If D3_CONTA < mv_par13 .Or. D3_CONTA > mv_par14
					dbSkip()
					Loop
				EndIF
				
				If D3_EMISSAO < mv_par16 .Or. D3_EMISSAO > mv_par17
					dbSkip()
					Loop
				EndIf
			#ENDIF	

			If UPPER(D3_DOC) < UPPER(mv_par21) .Or. UPPER(D3_DOC) > UPPER(mv_par22)
				dbSkip()
				Loop
			EndIf

			If !D3Valido()
				dbSkip()
				Loop
			EndIf
			
			If !Empty(aReturn[7]) .And. ! &(aReturn[7])
				dbSkip()
				Loop
			Endif

			If li > 58
				Cabec(titulo,cabec1,cabec2,wnrel,Tamanho,nTipo)
				lImprime := .T.
			EndIf
			
			If lImprime
				dbSelectArea("SB1")
				dbSeek(cFilial+SD3->D3_COD)
				If mv_par27 == 1
					If !lVersao
						lDescArm := SB2->(MsSeek(xFilial("SB2")+SD3->D3_COD+SD3->D3_LOCAL)) .And. !Empty(SB2->B2_LOCALIZ)
						cLocaliz := SB2->B2_LOCALIZ
					Else
						lDescArm := NNR->(MsSeek(xFilial("NNR")+SD3->D3_LOCAL)) .And. !Empty(NNR->NNR_DESCRI)
						cLocaliz := NNR->NNR_DESCRI						
					EndIf
				EndIf
				@ li,000 PSay STR0027+B1_COD+STR0028+SubStr(B1_DESC,1,30)+STR0029+B1_TIPO+STR0030+SD3->D3_GRUPO+STR0031+B1_UM+' '+IIF(lDescArm,STR0042+SD3->D3_LOCAL+" - "+cLocaliz,"")	//"Codigo : "###"   Descricao : "###"   Tipo : "###"   Grupo : "###"   UM : "##"   Armazem : "###############"
				IF mv_par18 == 1
					li += 2
				Endif
				dbSelectArea("SD3")
				lImprime := .F.
			EndIf
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Adiciona 1 ao contador de registros impressos         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			nCntImpr++
			nCusto := &(cCampoCus)
			nCM    := nCusto / Iif(D3_QUANT == 0,1,D3_QUANT)
			IF mv_par18 == 1
				@ Li,000 PSay D3_LOCAL
				If D3_TM <= "500"
					@ Li,013 PSay D3_QUANT Picture PesqPictQt("D3_QUANT",17)
				Else
					@ Li,037 PSay D3_QUANT Picture PesqPictQt("D3_QUANT",17)
				EndIf
				@ Li,053 PSay nCm			Picture PesqPict("SD3", "D3_CUSTO"+Str(mv_par15,1),17)
				@ Li,075 PSay nCusto		Picture PesqPict("SD3", "D3_CUSTO"+Str(mv_par15,1),17)
				@ Li,099 - If(nTamdoc > 13,  5, 0) PSay D3_TM 
				@ Li,109 - If(nTamdoc > 13,  7, 0) PSay D3_CF 
				@ Li,116 - If(nTamdoc > 13, 10, 0) PSay SubStr(D3_OP,1,6)  Picture "@!"
				@ Li,125 - If(nTamdoc > 13, 10, 0) PSay SubStr(D3_OP,7,2)  Picture "@!"
				@ Li,128 - If(nTamdoc > 13, 10, 0) PSay SubStr(D3_OP,9,3)  Picture "@!"
				@ Li,135 - If(nTamdoc > 13, 10, 0) PSay D3_NUMSEQ
				@ Li,142 - If(nTamdoc > 13, 10, 0) PSay D3_CC
				@ Li,163 - If(nTamdoc > 13, 10, 0) PSay D3_CONTA
				@ Li,184 - If(nTamdoc > 13, 10, 0) PSay D3_DOC
				@ Li,194 - If(nTamdoc > 13,  2, 0) PSay D3_EMISSAO
				@ Li,205 - If(nTamdoc > 13,  2, 0) PSay D3_USUARIO
				Li++
			Endif
			lPassou1 := .T.
			
			If D3_TM <= "500"
				nProdEnt  += D3_QUANT
				nProdVal  += nCusto
			Else
				nProdSai  += D3_QUANT
				nProdVal  -= nCusto
			EndIf
			nCntProd++
			dbSkip()
		EndDo
		
		If aReturn[8] != 1
			IF nCntProd > 1 .or. (mv_par18 == 2 .and. nCntProd > 0)
				Li++
				@ Li,013 PSay nProdEnt Picture PesqPictQt("D3_QUANT",17)
				@ Li,037 PSay nProdSai Picture PesqPictQt("D3_QUANT",17)
				@ Li,075 PSay nProdVal Picture PesqPict("SD3", "D3_CUSTO"+Str(mv_par15,1),17)
				If mv_par18 == 1
					@ Li,100 PSay STR0032 	//"TOT"
				Endif
				@ Li,120 PSay STR0033+SB1->B1_SEGUM+STR0034		//"Quantidade na Seg. UM ("###"): Entrada -> "
				nProdSegEnt :=ConvUm(SB1->B1_COD,nProdEnt,0,2)
				@ Li,159 PSay nProdSegEnt Picture PesqPict("SD3", "D3_CUSTO"+Str(mv_par15,1),17)
				@ Li,180 PSay STR0035	//"Saida -> "
				nProdSegSai :=ConvUm(SB1->B1_COD,nProdSai,0,2)
				@ Li,189 PSay nProdSegSai Picture PesqPict("SD3", "D3_CUSTO"+Str(mv_par15,1),17)
				Li++
				IF mv_par18 == 1
					@ Li,000 PSay __PrtThinLine()
					Li++
				Endif
			Endif
		ElseIf nCntProd == 1 .And. aReturn[8] != 1 .and. mv_par18 == 1
			@ Li,000 PSay __PrtThinLine()
			Li++
		EndIf
		
		nQuebraEnt += nProdEnt
		nQuebraSai += nProdSai
		nQuebraVal += nProdVal
		nQSegEnt   += nProdSegEnt
		nQSegSai   += nProdSegSai
		
	EndDo
	
	If lPassou1
		If aReturn[8] == 1
			Li++
			@ Li,013 PSay nQuebraEnt Picture PesqPictQt("D3_QUANT",17)
			@ Li,037 PSay nQuebraSai Picture PesqPictQt("D3_QUANT",17)
			@ Li,075 PSay nQuebraVal Picture PesqPict("SD3", "D3_CUSTO"+Str(mv_par15,1),17)
			@ Li,100 PSay  STR0032		//"TOT"
			@ Li,120 PSay STR0033+SB1->B1_SEGUM+STR0034		//"Quantidade na Seg. UM ("###"): Entrada -> "
			nQSegEnt := ConvUm(SB1->B1_COD,nQuebraEnt,0,2)
			@ Li,159 PSay nQSegEnt	Picture PesqPict("SD3", "D3_CUSTO"+Str(mv_par15,1),17)
			@ Li,180 PSay STR0035	//"Saida -> "
			nQSegSai := ConvUm(SB1->B1_COD,nQuebraSai,0,2)
			@ Li,189 PSay nQSegSai	Picture PesqPict("SD3", "D3_CUSTO"+Str(mv_par15,1),17)
			Li++
			@ Li,000 PSay __PrtThinLine()
			Li++
		Else
			@ Li,013 PSay nQuebraEnt Picture PesqPictQt("D3_QUANT",17)
			@ Li,037 PSay nQuebraSai Picture PesqPictQt("D3_QUANT",17)
			@ Li,075 PSay nQuebraVal Picture PesqPict("SD3", "D3_CUSTO"+Str(mv_par15,1),17)
			
			cDet := ''
			If aReturn[8] == 6 .Or. aReturn[8] == 7
				If aReturn[8]==6 .And. (aEntCt[2])->(dbSeek(xFilial(aEntCt[2])+cVar,.F.)) .Or. ;
					aReturn[8]==7 .And. (aEntCt[1])->(dbSeek(xFilial(aEntCt[1])+cVar,.F.))
					cDet := &(cEntCpo)
				ElseIf !Empty(cVar)
					cDet := STR0037 // ' ** CC nao cadastrado ** '
				EndIf
			EndIf
			
			@ Li,099 PSay cMens + cDet
			Li++
			
			@ Li,000 PSay __PrtThinLine()
			Li += 2
			
			dbSelectArea("SD3")
		EndIf
	EndIf
	
	nGeralEnt += nQuebraEnt
	nGeralSai += nQuebraSai
	nGeralVal += nQuebraVal
	nGSegEnt  += nQSegEnt
	nGSegSai  += nQSegSai
	
EndDo

If nCntImpr > 0
  	If li > 58
		Cabec(titulo,cabec1,cabec2,wnrel,Tamanho,nTipo)
	EndIf
	@ Li,013 PSay nGeralEnt Picture PesqPictQt("D3_QUANT",17)
	@ Li,037 PSay nGeralSai Picture PesqPictQt("D3_QUANT",17)
	@ Li,075 PSay nGeralVal Picture PesqPict("SD3", "D3_CUSTO"+Str(mv_par15,1),17)
	@ Li,099 PSay STR0036		//"<- TOTAL GERAL ->"
	@ Li,159 PSay nGSegEnt Picture PesqPict("SD3", "D3_CUSTO"+Str(mv_par15,1),17)
	@ Li,189 PSay nGSegSai Picture PesqPict("SD3", "D3_CUSTO"+Str(mv_par15,1),17)	
	Roda(nCntImpr,cRodaTxt,Tamanho)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Devolve as ordens originais do arquivo                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RetIndex("SD3")
dbClearFilter()
dbSetOrder(1)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Apaga indice de trabalho                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Ferase(cNomArq+OrdBagExt())

If aReturn[5] = 1
	Set Printer To
	dbCommitAll()
	OurSpool(wnrel)
Endif

MS_FLUSH()

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AjustaSX1 ³ Autor ³ Marcos V. Ferreira    ³ Data ³30.03.2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Cria as perguntas necesarias para o programa                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function AjustaSX1()
Local aArea := GetArea()
Local aHelpPor :={} 
Local aHelpEng :={} 
Local aHelpSpa :={} 
Local nTamSX1  :=Len(SX1->X1_GRUPO)
     
//Correcao de Ortografia do MV_PAR18
If SX1->(DbSeek (PADR("MTR300",nTamSX1)+"18"))
	RecLock("SX1",.F.)
	Replace SX1->X1_DEF01   With "Analitico"
	Replace SX1->X1_DEFSPA2 With "Analitico"
    Replace SX1->X1_DEF02   With "Sintetico"
    Replace SX1->X1_DEFSPA2 With "Sintetico"
	MsUnLock()
EndIf

PutSx1('MTR300','23','De Lote                      ?','¿De Lote                     ?','From Lot                     ?', ;  
'mv_chn','C',10,0,0,'G','','','','N','mv_par23','','','','','','','','','','','','','','','','', ;                                     
{'Lote Inicial a ser considerado na filtr ','agem da impressao do relatorio.         '}, ;                        
{'                                        ','                                        '}, ;                        
{'                                        ','                                        '},'')                                            //-- 36 - X1_HELP

PutSx1('MTR300','24','Ate Lote                     ?','¿A Lote                      ?','To Lot                       ?', ;  
'mv_cho','C',10,0,0,'G','','','','N','mv_par24','','','','ZZZZZZZZZZ','','','','','','','','','','','','', ;                                     
{'Lote Final a ser considerado na filtrage','m da impressao do relatorio.            '}, ;                        
{'                                        ','                                        '}, ;                        
{'                                        ','                                        '},'')                                            //-- 36 - X1_HELP

PutSx1('MTR300','25','De Sub-Lote                  ?','¿De Sub-Lote                 ?','From Sub-Lot                 ?', ;  
'mv_chp','C',6,0,0,'G','','','','N','mv_par25','','','','','','','','','','','','','','','','', ;                                     
{'Sub-Lote Inicial a ser considerado na fi','ltragem da impressao do relatorio.      '}, ;                        
{'                                        ','                                        '}, ;                        
{'                                        ','                                        '},'')                                            //-- 36 - X1_HELP

PutSx1('MTR300','26','Ate Sub-Lote                 ?','¿A Sub-Lote                  ?','To Sub-Lot                   ?', ;  
'mv_chq','C',6,0,0,'G','','','','N','mv_par26','','','','ZZZZZZ','','','','','','','','','','','','', ;                                     
{'Sub-Lote Final a ser considerado na filt','ragem da impressao do relatorio.        '}, ;                        
{'                                        ','                                        '}, ;                        
{'                                        ','                                        '},'')                                            //-- 36 - X1_HELP

/*-----------------------MV_PAR27--------------------------*/
Aadd( aHelpPor, "Imprime descricao do Armazem. Sim ou Nao" )

Aadd( aHelpEng, "Print warehouse description. Yes or No  " )

Aadd( aHelpSpa, "Imprime descripcion del almacen. Si o No" ) 

PutSx1( "MTR300","27","Imprime descricao do Armazem ?","Imprime descripc. del almacen?","Print warehouse description ?","mv_chr",;
"N",1,0,2,"C","","","","","mv_par27","Sim","Si","Yes","","Nao","No","No","","","","","","","","","",;
aHelpPor,aHelpEng,aHelpSpa)

If __lPyme 
	//Retirada as perguntas de 23 a 26 referente a rastreabilidade para a pyme
	If SX1->(DbSeek (PADR("MTR300",nTamSX1)+"23"))
		RecLock("SX1",.F.)
		Replace SX1->X1_PYME With "N"
		MsUnLock()
	EndIf
	If SX1->(DbSeek (PADR("MTR300",nTamSX1)+"24"))
		RecLock("SX1",.F.)
		Replace SX1->X1_PYME With "N"
		MsUnLock()
	EndIf
	If SX1->(DbSeek (PADR("MTR300",nTamSX1)+"25"))
		RecLock("SX1",.F.)
		Replace SX1->X1_PYME With "N"
		MsUnLock()
	EndIf
	If SX1->(DbSeek (PADR("MTR300",nTamSX1)+"26"))
		RecLock("SX1",.F.)
		Replace SX1->X1_PYME With "N"
		MsUnLock()
	EndIf		
Endif

If SX1->(DbSeek (PADR("MTR300",nTamSX1)+"21")) .And. SX1->X1_TAMANHO <> TamSX3('D1_DOC')[1]
	RecLock("SX1",.F.)
	Replace SX1->X1_TAMANHO With TamSX3('D1_DOC')[1]
	MsUnLock()
EndIf

If SX1->(DbSeek (PADR("MTR300",nTamSX1)+"22")) .And. SX1->X1_TAMANHO <> TamSX3('D1_DOC')[1]
	RecLock("SX1",.F.)
	Replace SX1->X1_TAMANHO With TamSX3('D1_DOC')[1]
	MsUnLock()
EndIf

If SX1->(DbSeek (PADR("MTR300",nTamSX1)+"16"))
	RecLock("SX1",.F.)
	Replace SX1->X1_PERSPA With "¿De Fecha ?"
	MsUnLock()
EndIf

RestArea(aArea)
Return

Static Function f_Footer(oReport, oSection2)
	n_Linha := oReport:Row()

	oReport:PrintText("______________________", n_Linha, oSection2:Cell("D3_LOCAL"):ColPos())
	oReport:PrintText("______________________", n_Linha, oSection2:Cell("D3_OP"):ColPos())
	oReport:PrintText("______________________", n_Linha, oSection2:Cell("D3_USUARIO"):ColPos())

	oReport:IncRow()
	n_Linha := oReport:Row()

	oReport:PrintText("     Conferente", n_Linha,oSection2:Cell("D3_LOCAL"):ColPos())
	oReport:PrintText("     Matrícula", n_Linha,oSection2:Cell("D3_OP"):ColPos())
	oReport:PrintText("     Data/Hora", n_Linha,oSection2:Cell("D3_USUARIO"):ColPos())
Return